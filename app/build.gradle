apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext {
    VERSION_CODE = 1
    VERSION_NAME = "0.1.0"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "me.barta.actdrawexplain"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode VERSION_CODE
        versionName "${VERSION_NAME}"
        setProperty("archivesBaseName", "actdrawexplain-${VERSION_NAME}")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en"

        lintOptions {
            checkReleaseBuilds true
            abortOnError false
        }

        dataBinding {
            enabled = true
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            zipAlignEnabled true
            jniDebuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug-${getGitHash()}"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    kaptTest 'com.google.dagger:dagger-compiler:2.10'

    // Android
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"
    //compile "com.android.support:cardview-v7:$support_lib_version"
    kapt "com.android.databinding:compiler:$gradle_version"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Dependency injection
    compile 'com.google.dagger:dagger:2.10'
    kapt 'com.google.dagger:dagger-compiler:2.10'
    provided 'javax.annotation:javax.annotation-api:1.3'

    // Views
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // RxJava & RxAndroid
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.9'

    // Database
    compile 'io.realm:android-adapters:2.1.0'

    // Graphics
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Logging
    compile 'com.jakewharton.timber:timber:4.5.1'
}